allprojects {
    apply plugin: "java"

    version = ext.project_version + "." + getBuildNumber() + (gitBranch() == "development" ? "-SNAPSHOT" : "")
	
    task sourcesJar(type: Jar, dependsOn: classes) {
        description = "Creates a jar file containing the sources for the project in the build directory."
        from sourceSets.main.allSource
        classifier = "sources"
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        description = "Creates a jar file containing the javadocs for the project in the build directory."
        from javadoc.destinationDir
        classifier = "javadoc"
    }

    task buildJar(type: Jar) {
        description = "Creates a standard jar file containing the compiled java classes for this library."
        from sourceSets.main.output
    }

    artifacts {
        archives buildJar
        archives sourcesJar
        archives javadocJar
    }

    repositories {
        jcenter()
    }
	
	dependencies {
        compile group: 'junit', name: 'junit', version: '4.12'
    }
}

group = ext.project_group
archivesBaseName = ext.project_name
description = "A Java library for discovering the meta data of files"

subprojects {
    dependencies {
        compile rootProject
    }
}

def gitBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

String getBuildNumber() {
    return System.getenv("BUILD_NUMBER") ?: System.getenv("TRAVIS_BUILD_NUMBER") ?: "0"
}